apply plugin: 'maven-publish'
apply from: "$gradle.ext.exoplayerSettingsDir/missing_aar_type_workaround.gradle"

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = "https://jfrog.fkinternal.com/artifactory/gradle_virtual/" + "libs-snapshots-local"
            }
        }
        publications {
            release(MavenPublication) {
                artifact androidSourcesJar
                groupId = 'com.google.android.exoplayer'
                artifactId = releaseArtifact
                version releaseVersion
                pom {
                    name = releaseArtifact
                    description = releaseDescription
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'The Android Open Source Project'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/google/ExoPlayer.git'
                        url = 'https://github.com/google/ExoPlayer'
                    }
                    withXml {
                        addMissingAarTypeToXml(it)
                    }
                }
            }
        }
    }
}
//tasks.withType(PublishToMavenRepository) { it.dependsOn lint, test }

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}